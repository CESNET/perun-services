#!/usr/bin/perl

use strict;
use warnings;
use perunServicesInit;
use perunServicesUtils;

our $SERVICE_NAME     = "vsup_google_groups";
our $PROTOCOL_VERSION = "3.0.0";
my $SCRIPT_VERSION = "3.0.0";

perunServicesInit::init;
my $DIRECTORY = perunServicesInit::getDirectory;
my $data = perunServicesInit::getHierarchicalData;

# Constants
our $A_F_GOOGLE_GROUP_DOMAIN;		*A_F_GOOGLE_GROUP_DOMAIN =			\'urn:perun:facility:attribute-def:def:googleGroupDomain';
our $A_R_GOOGLE_GROUP_NAME;				*A_R_GOOGLE_GROUP_NAME =		\'urn:perun:resource:attribute-def:def:googleGroupName';
our $A_R_GOOGLE_GROUP_DISPLAY_NAME;		*A_R_GOOGLE_GROUP_DISPLAY_NAME =\'urn:perun:resource:attribute-def:def:googleGroupDisplayName';
our $A_U_GOOGLE_GROUP_LOGIN;			*A_U_GOOGLE_GROUP_LOGIN =		\'urn:perun:user:attribute-def:def:login-namespace:vsup';
our $A_U_FIRST_NAME; 			*A_U_FIRST_NAME =			\'urn:perun:user:attribute-def:core:firstName';
our $A_U_LAST_NAME; 			*A_U_LAST_NAME =			\'urn:perun:user:attribute-def:core:lastName';
our $A_U_ARTISTIC_FIRST_NAME; 	*A_U_ARTISTIC_FIRST_NAME = 	\'urn:perun:user:attribute-def:def:artisticFirstName';
our $A_U_ARTISTIC_LAST_NAME; 	*A_U_ARTISTIC_LAST_NAME = 	\'urn:perun:user:attribute-def:def:artisticLastName';

# Global data structure

# $users->{$login}->{ATTR} = $attrValue;
our $users = {};
# $groups->{name}->{"displayName"} = nice name of a group;
# $groups->{name}->{"members"}->{$login} = 1;
our $groups = {};

my %facilityAttributes = attributesToHash $data->getAttributes;
if (!defined($facilityAttributes{$A_F_GOOGLE_GROUP_DOMAIN})) {
	exit 1;
}
my $domainName = $facilityAttributes{$A_F_GOOGLE_GROUP_DOMAIN};

#
# AGGREGATE DATA
#
# FOR EACH RESOURCE
foreach my $rData ($data->getChildElements) {

	my %resourceAttributes = attributesToHash $rData->getAttributes;

	my $groupName = $resourceAttributes{$A_R_GOOGLE_GROUP_NAME};
	my $groupDisplayName = $resourceAttributes{$A_R_GOOGLE_GROUP_DISPLAY_NAME};

	if (defined $groupName and length $groupName) {
		unless(exists $groups->{$groupName}) {
			$groups->{$groupName}->{"displayName"} = $groupDisplayName;
			$groups->{$groupName}->{"members"} = {};
		}
	}

	my @membersData = $rData->getChildElements;

	foreach my $member (@membersData) {

		my %uAttributes = attributesToHash $member->getAttributes;
		my $login = $uAttributes{$A_U_GOOGLE_GROUP_LOGIN};

		# create user entry
		$users->{$login}->{$A_U_FIRST_NAME} = ($uAttributes{$A_U_ARTISTIC_FIRST_NAME} || ($uAttributes{$A_U_FIRST_NAME} || ''));
		$users->{$login}->{$A_U_LAST_NAME} = ($uAttributes{$A_U_ARTISTIC_LAST_NAME} || ($uAttributes{$A_U_LAST_NAME} || ''));

		# put member in a group
		if (defined $groupName and length $groupName) {
			$groups->{$groupName}->{"members"}->{$login."\@".$domainName} = 1;
		}

	}

}

#
# PRINT DOMAIN FILE
#
my $fileNameDomain = $DIRECTORY . "vsup_google_groups_domain";
open FILE, ">$fileNameDomain" or die "Cannot open $fileNameDomain: $! \n";
print FILE $domainName;
close(FILE) or die "Cannot close $fileNameDomain: $! \n";

#
# PRINT USERS DATA FILE
#
my $usersFileName = "$DIRECTORY/$::SERVICE_NAME"."_users.csv";
open FILE,">$usersFileName" or die "Cannot open $usersFileName: $! \n";
binmode FILE, ":utf8";

my @keys = sort keys %{$users};
for my $key (@keys) {
	print FILE $key . "\@" . $domainName . ";" . $users->{$key}->{$A_U_FIRST_NAME} . ";" . $users->{$key}->{$A_U_LAST_NAME} . ";" . "\n";
}
close(FILE) or die "Cannot close $usersFileName: $! \n";

#
# PRINT GROUPS DATA FILE
#
my $groupsFileName = "$DIRECTORY/$::SERVICE_NAME"."_groups.csv";
open FILE,">$groupsFileName" or die "Cannot open $groupsFileName: $! \n";
binmode FILE, ":utf8";

my @groupKeys = sort keys %{$groups};
for my $key (@groupKeys) {
	my @userKeys = sort keys %{$groups->{$key}->{"members"}};
	print FILE $key . ";" . $groups->{$key}->{"displayName"} . ";" . join(',', @userKeys) . "\n";
}
close(FILE) or die "Cannot close $groupsFileName: $! \n";

perunServicesInit::finalize;
