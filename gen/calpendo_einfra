#!/usr/bin/perl

use strict;
use warnings;
use perunServicesInit;
use perunServicesUtils;
use Perun::Agent;
use Perun::GroupsAgent;
use open qw/:std :utf8/;
use Text::CSV;

local $::SERVICE_NAME = "calpendo_einfra";
local $::PROTOCOL_VERSION = "3.0.0";
my $SCRIPT_VERSION = "3.0.2";

perunServicesInit::init;
my $DIRECTORY = perunServicesInit::getDirectory;
my $data = perunServicesInit::getHashedHierarchicalData;

#Constants
my $VMCF_USERS_GROUP_ID = 14298;
our $A_USER_LOGIN;                *A_USER_LOGIN =            \'urn:perun:user:attribute-def:def:login-namespace:einfraid-persistent-shadow';
our $A_MEMBER_STATUS;             *A_MEMBER_STATUS =         \'urn:perun:member:attribute-def:core:status';
our $A_FIRST_NAME;                *A_FIRST_NAME =            \'urn:perun:user:attribute-def:core:firstName';
our $A_LAST_NAME;                 *A_LAST_NAME =             \'urn:perun:user:attribute-def:core:lastName';
our $A_EMAIL;                     *A_EMAIL =                 \'urn:perun:member:attribute-def:def:mail';
our $STATUS_VALID;                *STATUS_VALID =            \'VALID';
our $A_PHONE;                     *A_PHONE =                 \'urn:perun:member:attribute-def:def:phone';
our $A_VMCF_ORG_UNIT;             *A_VMCF_ORG_UNIT =         \'urn:perun:member:attribute-def:def:vmcfOrgUnit';
our $A_VMCF_USER_POSITION;        *A_VMCF_USER_POSITION =    \'urn:perun:member:attribute-def:def:vmcfUserPosition';
our $A_VMCF_UKCO;                 *A_VMCF_UKCO =             \'urn:perun:member:attribute-def:def:vmcfUKCO';
our $A_VMCF_UK_LOGIN;             *A_VMCF_UK_LOGIN =         \'urn:perun:member:attribute-def:def:vmcfUKlogin';

my $agent = perunServicesInit->getAgent;
my $groupsAgent = $agent->getGroupsAgent;
my %printed_logins;

my $csv = Text::CSV->new({ auto_diag => 1, eol => "\n", quote_char => undef, binary => 1 });
my $fileUsers = $DIRECTORY . "users.csv";
open my $fh,">$fileUsers" or die "Cannot open $fileUsers: $! \n";
$csv->print($fh,['LOGIN','EMAIL','FIRSTNAME','LASTNAME','PHONE','ORG_UNIT','USER_POSITION','UKCO','UK_LOGIN','SUBGROUPS']);

my $subgroupNameById = { map {
	$_->{_id} => ( $_->{_name} || '' ) =~ s/^[^:]*://r #name is stripped of top-level group
} $groupsAgent->getAllSubGroups( group => $VMCF_USERS_GROUP_ID ) };

my $subgroupMembersByGroupId = {};
foreach my $subgroupId (keys %{$subgroupNameById}) {
	my @subgroupMembers = $groupsAgent->getGroupMembers( group => $subgroupId );
	$subgroupMembersByGroupId->{$subgroupId} = [ map { $_->{_id} } @subgroupMembers ];
}

my $subgroupsByMember = {};
foreach my $subgroupId (keys %{$subgroupNameById}) {
	my $subgroup_shortname = $subgroupNameById->{$subgroupId};
	foreach my $memberId (@{ $subgroupMembersByGroupId->{$subgroupId} }) {
		push @{ $subgroupsByMember->{$memberId} }, $subgroup_shortname;
	}
}

foreach my $memberId ($data->getMemberIdsForFacility()) {
	my $status = $data->getMemberAttributeValue( member => $memberId, attrName => $A_MEMBER_STATUS );
	unless ($status eq $STATUS_VALID) {
		next;
	}

	my $login = $data->getUserAttributeValue( member => $memberId, attrName => $A_USER_LOGIN );
	my $f_name = $data->getUserAttributeValue( member => $memberId, attrName => $A_FIRST_NAME );
	my $l_name = $data->getUserAttributeValue( member => $memberId, attrName => $A_LAST_NAME );
	my $email =  $data->getMemberAttributeValue( member => $memberId, attrName => $A_EMAIL );
	my $phone =  $data->getMemberAttributeValue( member => $memberId, attrName => $A_PHONE );
	my $orgUnit = $data->getMemberAttributeValue( member => $memberId, attrName => $A_VMCF_ORG_UNIT );
	my $position = $data->getMemberAttributeValue( member => $memberId, attrName => $A_VMCF_USER_POSITION );
	my $ukco = $data->getMemberAttributeValue( member => $memberId, attrName => $A_VMCF_UKCO );
	my $ukLogin = $data->getMemberAttributeValue( member => $memberId, attrName => $A_VMCF_UK_LOGIN );
	my $subgroups_string = join ';', @{ $subgroupsByMember->{$memberId} || [] };

	unless (exists $printed_logins{$login}) {
		$printed_logins{$login} = 1;
		$csv->print($fh,[ $login, $email, $f_name, $l_name, $phone, $orgUnit, $position, $ukco, $ukLogin, $subgroups_string ] );
	}
}
close ($fh) or die "Cannot close $fileUsers: $! \n";

perunServicesInit::finalize;
