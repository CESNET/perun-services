#!/usr/bin/perl

use strict;
use warnings;
use perunServicesInit;
use perunServicesUtils;
use Perun::Agent;
use Date::Calc qw\Delta_Days\;
use open qw/:std :utf8/;
use JSON;
use utf8;

local $::SERVICE_NAME = "o365_life_cycle_manager_mu";
local $::PROTOCOL_VERSION = "1.1.0";

perunServicesInit::init;
my $DIRECTORY = perunServicesInit::getDirectory;
my $data = perunServicesInit::getDataWithGroups;

#forward declaration
sub processUsers;

#Headers
my $userIdHeader = "userId";
my $memberIdHeader = "memberId";
my $loginHeader = "uco";
my $affiliationsHeader = "affiliations";
my $preferredMailHeader = "preferredMail";
my $groupMembershipExpirationHeader = "groupMembershipExpiration";
my $o365EmailAddressesHeader = "o365EmailAddresses";
my $o365AllumniAccountEnabledHeader = "o365AllumniAccountEnabled";
my $o365PrivilegedLicenceHeader = "o365PrivilegedLicence";
my $eligibleForO365AlumniLicenceHeader = "eligibleForO365AlumniLicence";
my $o365GracePeriodExpirationHeader = "o365GracePeriodExpiration"; 

#Constants
our $A_USER_ID;                       *A_USER_ID =                       \'urn:perun:user:attribute-def:core:id';
our $A_MEMBER_ID;                     *A_MEMBER_ID =                     \'urn:perun:member:attribute-def:core:id';
our $A_USER_LOGIN_MU;                 *A_USER_LOGIN_MU =                 \'urn:perun:user:attribute-def:def:login-namespace:mu';
our $A_USER_PREFERRED_MAIL;           *A_USER_PREFERRED_MAIL =           \'urn:perun:user:attribute-def:def:preferredMail';
our $A_RESOURCE_AFFILIATIONS;         *A_RESOURCE_AFFILIATIONS =         \'urn:perun:resource:attribute-def:def:affiliation';
our $A_GROUP_MEMBERSHIP_EXPIRATION;   *A_GROUP_MEMBERSHIP_EXPIRATION =   \'urn:perun:member_group:attribute-def:def:groupMembershipExpiration';
our $A_MEMBER_O365_EMAIL_ADDRS_MU;    *A_MEMBER_O365_EMAIL_ADDRS_MU =    \'urn:perun:member:attribute-def:def:o365EmailAddresses:mu';
our $A_USER_FAC_ALLUMNI_ACC_ENABLED;  *A_USER_FAC_ALLUMNI_ACC_ENABLED =  \'urn:perun:user_facility:attribute-def:def:o365AlumniAccountEnabled';
our $A_USER_FAC_PRIVILEGED_LICENCE;   *A_USER_FAC_PRIVILEGED_LICENCE =   \'urn:perun:user_facility:attribute-def:def:o365PrivilegedLicence';
our $A_USER_FAC_ELIGIBLE_FOR_LICENCE; *A_USER_FAC_ELIGIBLE_FOR_LICENCE = \'urn:perun:user_facility:attribute-def:def:eligibleForO365AlumniLicence';
our $A_USER_FAC_GRACE_PERIOD_EXP;     *A_USER_FAC_GRACE_PERIOD_EXP =     \'urn:perun:user_facility:attribute-def:def:o365GracePeriodExpiration';

#it defines UNLIMITED expiration for purpose of this gen script
our $UNLIMITED_EXPIRATION = "UNLIMITED";
#it defines specific affilation or resource where we need to process expiration in groups
our $O365_GUEST_AFFILIATION = "o365guest";

my $userStruc = {};

#file to which we will export the data about users
my $file = $DIRECTORY . "/data.json";

#foreach resource in the structure
foreach my $resourceData ($data->getChildElements) {
	my %resourceAttributes = attributesToHash $resourceData->getAttributes;
	#defined affilation for whole resource
	my $affiliation = $resourceAttributes{$A_RESOURCE_AFFILIATIONS};

	#for each group assigned to the resource
	foreach my $groupData (($resourceData->getChildElements)[0]->getChildElements) {
		my $membersElement = ($groupData->getChildElements)[1];
		
		#and every member in such group
		for my $memberData ($membersElement->getChildElements) {
			my %memberAttributes = attributesToHash $memberData->getAttributes;
			my $userId = $memberAttributes{$A_USER_ID};
			my $expirationInGroup = $memberAttributes{$A_GROUP_MEMBERSHIP_EXPIRATION};

			#process every member|group|resource combination one by one
			processUsers $affiliation, $expirationInGroup, $memberData;	
		}
	}
}

# PREPARE USERSDATA TO JSON
my @users;
foreach my $userId (sort keys %$userStruc) {
	my $user = {};
	#use *1 to force perl to use integer instead of strings
	$user->{$userIdHeader} = $userId * 1;
	$user->{$memberIdHeader} = $userStruc->{$userId}->{$memberIdHeader} * 1;
	$user->{$loginHeader} = $userStruc->{$userId}->{$loginHeader};
	$user->{$preferredMailHeader} = $userStruc->{$userId}->{$preferredMailHeader};
	$user->{$o365EmailAddressesHeader} = $userStruc->{$userId}->{$o365EmailAddressesHeader};
	$user->{$affiliationsHeader} = $userStruc->{$userId}->{$affiliationsHeader};
	$user->{$o365AllumniAccountEnabledHeader} = $userStruc->{$userId}->{$o365AllumniAccountEnabledHeader};
	$user->{$o365PrivilegedLicenceHeader} = $userStruc->{$userId}->{$o365PrivilegedLicenceHeader};
	$user->{$eligibleForO365AlumniLicenceHeader} = $userStruc->{$userId}->{$eligibleForO365AlumniLicenceHeader};
	$user->{$o365GracePeriodExpirationHeader} = $userStruc->{$userId}->{$o365GracePeriodExpirationHeader};
	my $groupExpiration = $userStruc->{$userId}->{$groupMembershipExpirationHeader};
	#UNLIMITED expiration and not defined expirations are for purpose of JSON same => null
	unless($groupExpiration) {
		$user->{$groupMembershipExpirationHeader} = undef;
	} elsif ($groupExpiration eq $UNLIMITED_EXPIRATION) {
		$user->{$groupMembershipExpirationHeader} = undef;
	} else {
		$user->{$groupMembershipExpirationHeader} = $groupExpiration;
	}
	push @users, $user;
}
my %usersFinal = ();
$usersFinal{'users'} = \@users;

# PRINT USERS TO JSON
open FILE_USERS,">$file" or die "Cannot open $file: $! \n";
binmode(FILE_USERS);
print FILE_USERS JSON::XS->new->utf8->pretty->encode(\%usersFinal);
close (FILE_USERS) or die "Cannot close $file: $! \n";

perunServicesInit::finalize;

##############################################################################
#   Only subs definitions down there
##############################################################################
## creates structure for users.json
sub processUsers {
	my ($affiliation, $expirationInGroup, $memberData) = @_;

	my %memberAttributes = attributesToHash $memberData->getAttributes;
	my $userId = $memberAttributes{$A_USER_ID};

	#if user was already processed in another group, we can skip same attributes
	if (exists $userStruc->{$userId}) {
		#all these operations need to have defined affiliation
		if($affiliation) {
			my @affiliations = uniqList ($affiliation, @{$userStruc->{$userId}->{$affiliationsHeader}});
			$userStruc->{$userId}->{$affiliationsHeader} = \@affiliations;

			#we need to calculate expriration for guest resources
			if($affiliation eq $O365_GUEST_AFFILIATION) {
				my $lastDefinedExpiration = $userStruc->{$userId}->{$groupMembershipExpirationHeader};
				#if this new expiration is empty, it means is unlimited and we can set it without any more checks
				if(!$expirationInGroup) {
					$userStruc->{$userId}->{$groupMembershipExpirationHeader} = $UNLIMITED_EXPIRATION;
				#if new is not empty and last is empty, we can set a new one instead
				} elsif (!$lastDefinedExpiration) {
					$userStruc->{$userId}->{$groupMembershipExpirationHeader} = $expirationInGroup;
				#if both are not empty, but last is unlimited, we can skip it, in other case we need to choose the biggest
				#for example: 1.1.2001 > 1.1.2000
				} elsif ($lastDefinedExpiration ne $UNLIMITED_EXPIRATION) {
					my ($lastYear, $lastMonth, $lastDay) = unpack "A4xA2xA2", $lastDefinedExpiration;
					my ($newYear, $newMonth, $newDay) = unpack "A4xA2xA2", $expirationInGroup;
					my $diff = Delta_Days($lastYear, $lastMonth, $lastDay, $newYear, $newMonth, $newDay);
					#change only if this new groupExpiration is bigger then the old one
					if($diff>0) {
						$userStruc->{$userId}->{$groupMembershipExpirationHeader} = $expirationInGroup;
					}
				}
			}
		}
	#this is the first time user as occured in any processed group so we need to create a new full record
	} else {
		my $user = {};
		my $memberId = $memberAttributes{$A_MEMBER_ID};
		my $login = $memberAttributes{$A_USER_LOGIN_MU};
		my $preferredMail = $memberAttributes{$A_USER_PREFERRED_MAIL};
		my $o365EmailAddresses = $memberAttributes{$A_MEMBER_O365_EMAIL_ADDRS_MU};
		my $o365AllumniAccountEnabled = $memberAttributes{$A_USER_FAC_ALLUMNI_ACC_ENABLED};
		my $o365GracePeriodExpiration = $memberAttributes{$A_USER_FAC_GRACE_PERIOD_EXP};
		my $eligibleForO365AlumniLicence = $memberAttributes{$A_USER_FAC_ELIGIBLE_FOR_LICENCE};
		my $o365PrivilegedLicence = $memberAttributes{$A_USER_FAC_PRIVILEGED_LICENCE};

		$userStruc->{$userId}->{$memberIdHeader} = $memberId;
		$userStruc->{$userId}->{$loginHeader} = $login;
		$userStruc->{$userId}->{$preferredMailHeader} = $preferredMail;
		$userStruc->{$userId}->{$o365EmailAddressesHeader} = $o365EmailAddresses;
		$userStruc->{$userId}->{$o365AllumniAccountEnabledHeader} = $o365AllumniAccountEnabled ? JSON::true : JSON::false;
		$userStruc->{$userId}->{$eligibleForO365AlumniLicenceHeader} = $eligibleForO365AlumniLicence ? JSON::true : JSON::false;
		$userStruc->{$userId}->{$o365PrivilegedLicenceHeader} = $o365PrivilegedLicence ? JSON::true : JSON::false;
		$userStruc->{$userId}->{$o365GracePeriodExpirationHeader} = $o365GracePeriodExpiration;

		#we need to set correct group expiration if affiliation is for guests
		if($affiliation && $affiliation eq $O365_GUEST_AFFILIATION) {
			#if expiration is not empty, we will set it as is
			if($expirationInGroup) {
				$userStruc->{$userId}->{$groupMembershipExpirationHeader} = $expirationInGroup;
			#if expiration is empty, we will use constat instead to be able to recognize between undef and unlimited there
			} else {
				$userStruc->{$userId}->{$groupMembershipExpirationHeader} = $UNLIMITED_EXPIRATION;
			}
		#if this is not guest affiliation, we can set undef instead
		} else {
			$userStruc->{$userId}->{$groupMembershipExpirationHeader} = undef;
		}

		#affiliations need to be array, because one user can have more affiliations by resources
		my @affiliations = ();
		if($affiliation) {
			push @affiliations, $affiliation;
		}
		$userStruc->{$userId}->{$affiliationsHeader} = \@affiliations;
	}
}
