#!/usr/bin/perl

use strict;
use warnings;
use perunServicesInit;
use perunServicesUtils;
use Perun::Agent;
use Perun::GroupsAgent;
use open qw/:std :utf8/;

local $::SERVICE_NAME = "drupal_elixir";
local $::PROTOCOL_VERSION = "3.1.0";
my $SCRIPT_VERSION = "3.3.0";

perunServicesInit::init;
my $DIRECTORY = perunServicesInit::getDirectory;
my $data = perunServicesInit::getHashedDataWithGroups;

#forward declaration
sub processUsers;
sub processGroups;
sub processMemberships;

#Constants
our $A_USER_ID;                   *A_USER_ID =               \'urn:perun:user:attribute-def:core:id';
our $A_USER_LOGIN;                *A_USER_LOGIN =            \'urn:perun:user:attribute-def:def:login-namespace:lifescienceid-username';
our $A_USER_STATUS;               *A_USER_STATUS =           \'urn:perun:member:attribute-def:core:status';
our $A_MEMBER_IS_SUSPENDED;       *A_MEMBER_IS_SUSPENDED =   \'urn:perun:member:attribute-def:virt:isSuspended';
our $A_USER_EMAIL;                *A_USER_EMAIL =            \'urn:perun:user:attribute-def:def:preferredMail';
our $A_USER_LS_ID;                *A_USER_LS_ID =            \'urn:perun:user:attribute-def:virt:login-namespace:lifescienceid-persistent';
our $A_USER_D_NAME;               *A_USER_D_NAME =           \'urn:perun:user:attribute-def:core:displayName';
our $A_USER_F_NAME;               *A_USER_F_NAME =           \'urn:perun:user:attribute-def:core:firstName';
our $A_USER_L_NAME;               *A_USER_L_NAME =           \'urn:perun:user:attribute-def:core:lastName';
our $A_GROUP_ID;                  *A_GROUP_ID =              \'urn:perun:group:attribute-def:core:id';
our $A_GROUP_NAME;                *A_GROUP_NAME =            \'urn:perun:group:attribute-def:core:name';
our $A_PARENT_GROUP_ID;           *A_PARENT_GROUP_ID =       \'urn:perun:group:attribute-def:core:parentGroupId';
our $A_GROUP_TYPE;                *A_GROUP_TYPE =            \'urn:perun:group_resource:attribute-def:def:drupalGroupType';
our $A_M_TYPE_MEMBER;             *A_M_TYPE_MEMBER =         \'member';
our $A_M_TYPE_ADMIN;              *A_M_TYPE_ADMIN =          \'admin';

our $STATUS_VALID;                *STATUS_VALID =            \'VALID';
our $STATUS_EXPIRED;              *STATUS_EXPIRED =          \'EXPIRED';
our $STATUS_SUSPENDED;            *STATUS_SUSPENDED =        \'SUSPENDED';

our $USER_NAME_INVALID;           *USER_NAME_INVALID =       \'invalid';
our $USER_NAME_VALID;             *USER_NAME_VALID =         \'valid';

our $userStruc = {};
our $uLogin = {};
our $uStatus = {};
our $uEmail = {};
our $uName = {};
our $uFirstName = {};
our $uLastName = {};
our $uLsId = {};

our $duplicityUserStruc = {};
our $invalidNamesUserStruc = {};
our $lowerCaseEmailStruc = {};

our $groupStruc = {};
our $gName = {};
our $gType = {};
our $gParentId = {};

our $membershipStruc = {};

my $fileUsers = $DIRECTORY . "users.csv";
my $fileGroups = $DIRECTORY . "groups.csv";
my $fileMemberships = $DIRECTORY . "memberships.csv";
my $fileUsersDuplicities = $DIRECTORY . "users-duplicities.txt";
my $fileUserInvalidNames = $DIRECTORY . "users-invalid-names.txt";


my $agent = perunServicesInit->getAgent;
my $groupAgent = $agent->getGroupsAgent;

foreach my $resourceId ( $data->getResourceIds() ) {
	foreach my $groupId ( $data->getGroupIdsForResource( resource => $resourceId ) ) {
		processGroups $groupId, $resourceId;
	}
}

# USERS: user_id login status e-mail display_name
open FILE_USERS,">$fileUsers" or die "Cannot open $fileUsers: $! \n";
foreach my $uid (sort keys %$userStruc) {
	my $login = $userStruc->{$uid}->{$uLogin};
	my $status = $userStruc->{$uid}->{$uStatus};
	my $email = $userStruc->{$uid}->{$uEmail};
	my $name = $userStruc->{$uid}->{$uName};
	my $lsId = $userStruc->{$uid}->{$uLsId};
	my $firstName = $userStruc->{$uid}->{$uFirstName};
	my $lastName = $userStruc->{$uid}->{$uLastName};

	print FILE_USERS "$uid,$login,$status,$email,$name,$lsId,$firstName,$lastName\n";
}
close (FILE_USERS) or die "Cannot close $fileUsers: $! \n";

# GROUPS: group_id group_name type parent_id
open FILE_GROUPS,">$fileGroups" or die "Cannot open $fileGroups: $! \n";
foreach my $gid (sort keys %$groupStruc) {
	my $name = $groupStruc->{$gid}->{$gName};
	my $type = $groupStruc->{$gid}->{$gType};
	my $parentId = $groupStruc->{$gid}->{$gParentId} ? $groupStruc->{$gid}->{$gParentId} : 0;
	print FILE_GROUPS "$gid,$name,$type,$parentId\n";
}
close (FILE_GROUPS) or die "Cannot close $fileGroups: $! \n";

# MEMBERSHIPS: user_id group_id membership
open FILE_MEMBERSHIPS,">$fileMemberships" or die "Cannot open $fileMemberships: $! \n";
foreach my $uid (sort keys %$membershipStruc) {
	foreach my $gid (sort keys %{$membershipStruc->{$uid}}){
		# If the user is member and also admin in the group, then store only admin role
		my $membershipType = "";
		foreach my $membershipTypeTmp (sort keys %{$membershipStruc->{$uid}->{$gid}}){
			$membershipType = $membershipTypeTmp;
			if ($membershipTypeTmp eq $A_M_TYPE_ADMIN) {
				last;
			}
		}
		print FILE_MEMBERSHIPS "$uid,$gid,$membershipType\n";
	}
}
close (FILE_MEMBERSHIPS) or die "Cannot close $fileMemberships: $! \n";

# USERS-duplicities: $uid login e-mail
open FILE_USERS_DUPLICITIES,">$fileUsersDuplicities" or die "Cannot open $fileUsersDuplicities: $! \n";
foreach my $uid (sort keys %$duplicityUserStruc) {
	my $login = $duplicityUserStruc->{$uid}->{$uLogin};
	my $email = $duplicityUserStruc->{$uid}->{$uEmail};
	print FILE_USERS_DUPLICITIES "UID:$uid, LOGIN:$login, EMAIL:$email\n";
}
close (FILE_USERS_DUPLICITIES) or die "Cannot close $fileUsersDuplicities: $! \n";

# USERS-invalid name: uid login e-mail firstname lastname
open FILE_USERS_INVALID_NAMES,">$fileUserInvalidNames" or die "Cannot open $fileUserInvalidNames: $! \n";
foreach my $uid (sort keys %$invalidNamesUserStruc) {
	my $login = $invalidNamesUserStruc->{$uid}->{$uLogin};
	my $email = $invalidNamesUserStruc->{$uid}->{$uEmail};
	my $firstName = $invalidNamesUserStruc->{$uid}->{$uFirstName};
	my $lastName = $invalidNamesUserStruc->{$uid}->{$uLastName};
	my $displayName = $invalidNamesUserStruc->{$uid}->{$uName};
	print FILE_USERS_INVALID_NAMES "UID:$uid, LOGIN:$login, EMAIL:$email, F_NAME:$firstName, L_NAME:$lastName, D_NAME:$displayName\n";
}
close (FILE_USERS_INVALID_NAMES) or die "Cannot close $fileUserInvalidNames: $! \n";

perunServicesInit::finalize;


##############################################################################
#   Only subs definitions down there
##############################################################################
## creates structure for users.csv file
sub processUsers {
	my ($groupId, $memberId, $admins_ref) = @_;
	my @admins = @$admins_ref;
	my $user_name_validity = $USER_NAME_VALID;

	my $uid = $data->getUserAttributeValue( member => $memberId, attrName => $A_USER_ID );
	my $login = $data->getUserAttributeValue( member => $memberId, attrName => $A_USER_LOGIN );
	my $status = $data->getMemberAttributeValue( member => $memberId, attrName => $A_USER_STATUS );
	my $lsId = $data->getUserAttributeValue( member => $memberId, attrName => $A_USER_LS_ID );

	if ($data->getMemberAttributeValue( member => $memberId, attrName => $A_MEMBER_IS_SUSPENDED )) {
		$status = $STATUS_SUSPENDED;
	}
	my $email = $data->getUserAttributeValue( member => $memberId, attrName => $A_USER_EMAIL );

	my $displayName = $data->getUserAttributeValue( member => $memberId, attrName => $A_USER_D_NAME );
	if (not defined($displayName)) {
		$displayName = "";
		$user_name_validity = $USER_NAME_INVALID;
	}

	my $firstName = $data->getUserAttributeValue( member => $memberId, attrName => $A_USER_F_NAME );
	if (not defined($firstName)) {
		$firstName = "";
		$user_name_validity = $USER_NAME_INVALID;
	}

	my $lastName = $data->getUserAttributeValue( member => $memberId, attrName => $A_USER_L_NAME );
	if (not defined($lastName)) {
		$lastName = "";
		$user_name_validity = $USER_NAME_INVALID;
	}

	if ($user_name_validity eq $USER_NAME_INVALID) {
		$invalidNamesUserStruc->{$uid}->{$uLogin} = $login;
		$invalidNamesUserStruc->{$uid}->{$uEmail} = $email;
		$invalidNamesUserStruc->{$uid}->{$uFirstName} = $firstName;
		$invalidNamesUserStruc->{$uid}->{$uLastName} = $lastName;
		$invalidNamesUserStruc->{$uid}->{$uName} = $displayName;
		return;
	}

	if (exists $userStruc->{$uid}) {
		my $memberStatus = $userStruc->{$uid}->{$uStatus};

		if ($memberStatus eq $STATUS_EXPIRED && $status eq $STATUS_VALID){
			# change from EXPIRED to VALID
			$userStruc->{$uid}->{$uStatus} = $status;
		} elsif ($memberStatus eq $STATUS_SUSPENDED && $status eq $STATUS_VALID){
			# change from SUSPENDED to VALID
			$userStruc->{$uid}->{$uStatus} = $status;
		} elsif ($memberStatus eq $STATUS_SUSPENDED && $status eq $STATUS_EXPIRED){
			# change from SUSPENDED to EXPIRED
			$userStruc->{$uid}->{$uStatus} = $status;
		}
	} else {
		my $lowerCaseEmail = lc($email);
		#duplicity found, create record of duplicity accounts
		if (exists $lowerCaseEmailStruc->{$lowerCaseEmail}) {
			$duplicityUserStruc->{$uid}->{$uLogin} = $login;
			$duplicityUserStruc->{$uid}->{$uEmail} = $email;
		} else {
			$userStruc->{$uid}->{$uLogin} = $login;
			$userStruc->{$uid}->{$uStatus} = $status;
			$userStruc->{$uid}->{$uEmail} = $email;
			$userStruc->{$uid}->{$uName} = $displayName;
			$userStruc->{$uid}->{$uFirstName} = $firstName;
			$userStruc->{$uid}->{$uLastName} = $lastName;
			$userStruc->{$uid}->{$uLsId} = $lsId;
			$lowerCaseEmailStruc->{$lowerCaseEmail} = 1;
		}
	}

	if(@admins) {
		foreach my $user (@admins){
			if ($user->getId() == $uid) {
				processMemberships $groupId, $uid, $A_M_TYPE_ADMIN;
			}
		}
	}

	processMemberships $groupId, $uid, $A_M_TYPE_MEMBER;
}

## creates structure for groups.csv file
sub processGroups {
	my ($groupId, $resourceId) = @_;

	if($data->getGroupAttributeValue( group => $groupId, attrName => $A_GROUP_NAME )) {
		my $groupName = $data->getGroupAttributeValue( group => $groupId, attrName => $A_GROUP_NAME );
		my $groupParentID = $data->getGroupAttributeValue( group => $groupId, attrName => $A_PARENT_GROUP_ID );
		my $groupType;

		if($data->getGroupResourceAttributeValue( group => $groupId, resource=> $resourceId, attrName => $A_GROUP_TYPE )) {
			$groupType = $data->getGroupResourceAttributeValue( group => $groupId, resource=> $resourceId, attrName => $A_GROUP_TYPE );
		} else {
			$groupType = "public";
		}

		unless(exists $groupStruc->{$groupId}) {
			$groupStruc->{$groupId}->{$gName} = $groupName;
			$groupStruc->{$groupId}->{$gType} = $groupType;
			$groupStruc->{$groupId}->{$gParentId} = $groupParentID;
		}

		my @admins = $groupAgent->getAdmins(group => $groupId);

		for my $memberId ($data->getMemberIdsForResourceAndGroup( resource=> $resourceId, group => $groupId )) {
			processUsers $groupId, $memberId, \@admins;
		}
	}
}

## creates structure for memberships.csv file
sub processMemberships {
	my ($gid, $uid, $membershipType) = @_;

	unless(exists $membershipStruc->{$uid}->{$gid}->{$membershipType}) {
		$membershipStruc->{$uid}->{$gid}->{$membershipType} = {};
	}
}
