#!/usr/bin/perl

use strict;
use warnings;
use perunServicesInit;
use perunServicesUtils;

our $SERVICE_NAME     = "google_groups";
our $PROTOCOL_VERSION = "3.0.0";
my $SCRIPT_VERSION = "3.0.0";

perunServicesInit::init;
my $DIRECTORY = perunServicesInit::getDirectory;
my $data      = perunServicesInit::getDataWithGroups;

#Constants
our $A_FACILITY_GOOGLE_NAMESPACE;		*A_FACILITY_GOOGLE_NAMESPACE =			\'urn:perun:facility:attribute-def:def:googleGroupNameNamespace';
our $A_GROUP_RESOURCE_GOOGLE_GROUP_NAME;	*A_GROUP_RESOURCE_GOOGLE_GROUP_NAME =		\'urn:perun:group_resource:attribute-def:virt:googleGroupName';
our $A_USER_GOOGLE_NAMESPACE;			*A_USER_GOOGLE_NAMESPACE =			\'urn:perun:user:attribute-def:virt:logins-namespace:google';

#Global data structure
our $groupStruc = {};

my %facilityAttributes = attributesToHash $data->getAttributes;
my $domainName = $facilityAttributes{$A_FACILITY_GOOGLE_NAMESPACE};

foreach my $resourceData ( $data->getChildElements ) {
	foreach my $groupData (($resourceData->getChildElements)[0]->getChildElements){
		my %groupAttributes = attributesToHash $groupData->getAttributes;
		my $membersElement = ($groupData->getChildElements)[1];

		if($groupAttributes{$A_GROUP_RESOURCE_GOOGLE_GROUP_NAME}) {
			my $groupName = $groupAttributes{$A_GROUP_RESOURCE_GOOGLE_GROUP_NAME} . "@" . $domainName;

			foreach my $memberData($membersElement->getChildElements) {
				my %memberAttributes = attributesToHash $memberData->getAttributes;
				my @logins = @{$memberAttributes{$A_USER_GOOGLE_NAMESPACE}};

				# skip users without google login, might happen, when user removes his UserExtSource
				# since google login is virtual attribute calculated from Google IdP UES.
				if (@logins) {
					foreach my $member(@logins){
						unless(exists $groupStruc->{$groupName}->{$member}) {
							$groupStruc->{$groupName}->{$member} = {};
						}
					}
				}
			}
		}
	}
}

my $fileName = "$DIRECTORY/$::SERVICE_NAME" . "_groups.csv";
open FILE, ">$fileName" or die "Cannot open $fileName: $! \n";

# output of google_groups_groups.csv file:
# - groupName;groupDisplayName;id1,id2,id3 where
# groupName is 'group1@domain.name.com'
# groupDisplayName is optional and unused here
# id[x] is a list of group member identifiers

# output of google_groups_domain file:
# - there is only one line containing name of the domain

foreach my $group (sort keys %$groupStruc) {
	print FILE $group, ";;", join(',', sort keys %{$groupStruc->{$group}}), "\n";
}

close(FILE) or die "Cannot close $fileName: $! \n";

my $fileNameDomain = $DIRECTORY . "google_groups_domain";
open FILE, ">$fileNameDomain" or die "Cannot open $fileNameDomain: $! \n";
print FILE $domainName;
close(FILE) or die "Cannot close $fileNameDomain: $! \n";

perunServicesInit::finalize;
